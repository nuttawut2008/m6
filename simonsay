#include <LiquidCrystal_I2C.h>
LiquidCrystal_I2C lcd(0x27,16,2); // LCD I2C address 0x27, size 16x2

int buttons[4] = {2, 3, 4, 5};
int leds[4] = {8, 9, 10, 11};

boolean button[4] = {0, 0, 0, 0};

#define buzzer 7
#define NOTE_C4  262
#define NOTE_CS4 277
#define NOTE_D4  294
#define NOTE_DS4 311
#define NOTE_E4  330
#define NOTE_F4  349
#define NOTE_FS4 370
#define NOTE_G4  392
#define NOTE_GS4 415
#define NOTE_A4  440
#define NOTE_AS4 466
#define NOTE_B4  494
#define NOTE_C5  523
#define NOTE_CS5 554
#define NOTE_D5  587
#define NOTE_DS5 622
#define NOTE_E5  659
#define NOTE_F5  698
#define NOTE_FS5 740
#define NOTE_G5  784
#define NOTE_GS5 830
#define NOTE_A5  880
#define NOTE_AS5 932
#define NOTE_B5  988
#define NOTE_C6  1047
#define NOTE_D6  1175
#define NOTE_E6  1319

#define levelsInGame 50
int bt_simonSaid[100];
int led_simonSaid[100];

boolean lost;
int game_play, level, stage;

void setup() {
  Serial.begin(9600);
  for(int i=0; i<4; i++) {
    pinMode(buttons[i], INPUT_PULLUP);
    pinMode(leds[i], OUTPUT);
  }
  pinMode(buzzer, OUTPUT);

  lcd.init();
  lcd.backlight();
  lcd.setCursor(0,0);
  lcd.print("   Welcome To   ");
  lcd.setCursor(0,1);
  lcd.print("RNA Memory Hero");
  delay(2000);
  lcd.clear();

  randomSeed(analogRead(0));
}

void playMelodyStep(int note, int duration) {
  tone(buzzer, note, duration);
  delay(duration * 1.3);
  noTone(buzzer);
}

void playExcitingTheme() {
  int melody[] = {
    NOTE_E5, NOTE_E5, 0, NOTE_E5,
    0, NOTE_C5, NOTE_E5, 0,
    NOTE_G5, 0, 0, 0,
    NOTE_G4, 0, 0, 0,
    NOTE_C5, 0, 0, NOTE_G4,
    0, 0, NOTE_E4, 0,
    0, NOTE_A4, 0, NOTE_B4,
    0, NOTE_AS4, NOTE_A4, 0,
    NOTE_G4, NOTE_E5, NOTE_G5,
    NOTE_A5, 0, NOTE_F5, NOTE_G5,
    0, NOTE_E5, 0, NOTE_C5,
    NOTE_D5, NOTE_B4, 0, 0
  };
  int durations[] = {
    125, 125, 125, 125,
    125, 125, 125, 125,
    375, 125, 125, 125,
    375, 125, 125, 125,
    375, 125, 125, 375,
    125, 125, 375, 125,
    125, 375, 125, 375,
    125, 125, 375, 125,
    125, 125, 375,
    375, 125, 125, 375,
    125, 125, 125, 125,
    375, 375, 125, 125
  };
  int numNotes = sizeof(melody) / sizeof(melody[0]);

  for (int i = 0; i < numNotes; i++) {
    if (digitalRead(buttons[0]) == LOW) break;
    if (melody[i] != 0) tone(buzzer, melody[i], durations[i]);
    int randomLed = random(0, 4);
    digitalWrite(leds[randomLed], HIGH);
    delay(durations[i]);
    digitalWrite(leds[randomLed], LOW);
    noTone(buzzer);
    delay(10);
  }
}

void excitementEffect() {
  for (int i=0; i<3; i++) {
    for (int j=0; j<4; j++) digitalWrite(leds[j], HIGH);
    tone(buzzer, 1000, 150);
    delay(150);
    for (int j=0; j<4; j++) digitalWrite(leds[j], LOW);
    tone(buzzer, 600, 150);
    delay(150);
  }
  noTone(buzzer);
}

void showScore(int score) {
  for(int i = 0; i < 6; i++) {
    lcd.clear();
    if (i % 2 == 0) {
      lcd.setCursor(0,0);
      lcd.print("!!! SCORE !!!");
      lcd.setCursor(0,1);
      lcd.print(score);
    }
    for(int j = 0; j < 4; j++) {
      digitalWrite(leds[j], (i % 2 == 0) ? HIGH : LOW);
    }
    tone(buzzer, 1000 + i*100, 100);
    delay(300);
  }
  noTone(buzzer);
  for(int j = 0; j < 4; j++) digitalWrite(leds[j], LOW);
  delay(300);
}

void loop() {
  switch(stage) {
    case 0:
      lcd.setCursor(0, 0); lcd.print("Press Red Button");
      lcd.setCursor(0, 1); lcd.print(" for Start Game ");
      while (digitalRead(buttons[0]) == HIGH) playExcitingTheme();
      level = 1; stage = 1; game_play = 1;
      break;

    case 1:
      excitementEffect();
      lcd.clear();
      lcd.setCursor(4,0); lcd.print("Level: "); lcd.print(level);
      lcd.setCursor(0,1); lcd.print(" -- Memorize -- ");
      delay(1500);
      int newLed;
      do {
        newLed = random(0, 4);
      } while (level > 1 && newLed == led_simonSaid[level - 1]);
      led_simonSaid[level] = newLed;
      for(int i=1; i<=level; i++) {
        digitalWrite(leds[led_simonSaid[i]], HIGH);
        playBuzzer(led_simonSaid[i]+1);
        digitalWrite(leds[led_simonSaid[i]], LOW);
        delay(400);
      }
      delay(500);
      stage=2;
      break;

    case 2:
      stage=3;
      lcd.setCursor(0, 1); lcd.print("   -- Play --   ");
      break;

    case 3:
      for(int i=0; i<4; i++){
        if(digitalRead(buttons[i]) == LOW){
          bt_simonSaid[game_play] = i;
          digitalWrite(leds[i], HIGH);
          playBuzzer(i+1);
          while(digitalRead(buttons[i]) == LOW);
          delay(50);
          digitalWrite(leds[i], LOW);
          game_play++;
          if(game_play-1==level){
            game_play=1;
            stage=4;
            break;
          }
        }
      }
      delay(10);
      break;

    case 4:
      lcd.setCursor(0, 1); lcd.print("  Verification  ");
      delay(1000);
      lost = false;
      for(int i=1; i<=level; i++) {
        if(led_simonSaid[i] != bt_simonSaid[i]) {
          lost = true;
          break;
        }
      }
      stage = lost ? 5 : 6;
      break;

    case 5:
      lcd.setCursor(0, 1); lcd.print(" !! You Lost !! ");
      tone(buzzer, 350);
      for(int i=0; i<4; i++) digitalWrite(leds[i], HIGH);
      delay(1000);
      lcd.setCursor(0, 1); lcd.print("!! GAME  OVER !!");
      noTone(buzzer);
      delay(1000);
      for(int i=0; i<4; i++) digitalWrite(leds[i], LOW);
      level=1; stage=0; lost=0;
      break;

    case 6:
      excitementEffect();
      showScore(level);
      lcd.setCursor(0, 1); lcd.print(" ** You  Win ** ");
      delay(1000);
      if(level == levelsInGame){
        lcd.setCursor(0, 0); lcd.print("Congratulation");
        lcd.setCursor(0, 1); lcd.print(" Level Complete");
        delay(1000);
        lcd.clear();
        level = 1;
      } else {
        level++;
      }
      stage = 1;
      break;
  }
}

void playBuzzer(int x) {
  tone(buzzer, 650+(x*100));
  delay(300);
  noTone(buzzer);
}
